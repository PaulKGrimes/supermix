#!/bin/sh
gnuplot << EOF

set grid

set terminal pngcairo solid color enhanced size 1024, 768
set output "$1.png"

set multiplot

set origin 0.1, 0.5
set size 0.4, 0.4
set title "Gain"
set xlabel "Bias Voltage (mV)"
set ylabel "Gain (dB)"
set y2label "Sideband Ratio (USB/LSB)"
set key bottom right
set y2tics
set y2range [0.8:1.2]
plot [:][-15:] "$1.dat" using 1:(20*log10(\$2)) title "LSB Gain" with lines, \
               "$1.dat" using 1:(20*log10(\$3)) title "USB Gain" with lines, \
               "$1.dat" using 1:(10*log10(\$2*\$2+\$3*\$3)) title "DSB Gain" with lines, \
               "$1.dat" using 1:(\$3/\$2) axes x1y2 title "Sideband Ratio" with lines
unset y2label
unset y2tics

set origin 0.5, 0.5
set size 0.4, 0.4
set title "Noise Temp"
set key top right
set ylabel "Noise (K)"
plot [:][0:100] "$1.dat" using 1:((\$4)/(\$2*\$2)) title "LSB Noise" with lines, \
	"$1.dat" using 1:((\$4)/(\$3*\$3)) title "USB Noise" with lines, \
    "$1.dat" using 1:((\$4)/(\$2*\$2+\$3*\$3)) title "DSB Noise" with lines, \
	"$1.dat" using 1:((297-(\$6/\$5)*77)/((\$6/\$5)-1)) title "YFact Noise" with lines

set origin 0.1, 0.1
set size 0.4, 0.4
set title "DC Current"
set key top left
set ylabel "Current (uA)"
plot "$1.dat" using 1:7 title "J1" with lines, \
	 "$1.dat" using 1:8 title "J2" with lines, \
     "$1.dat" using 1:9 title "J3" with lines, \
     "$1.dat" using 1:10 title "J4" with lines



set origin 0.5, 0.1
set size 0.4, 0.4
set title "DC Voltage"
set ylabel "DC Voltage (mV)"
plot "$1.dat" using 1:11 title "J1" with lines, \
	 "$1.dat" using 1:12 title "J2" with lines, \
     "$1.dat" using 1:13 title "J3" with lines, \
     "$1.dat" using 1:14 title "J4" with lines

EOF
